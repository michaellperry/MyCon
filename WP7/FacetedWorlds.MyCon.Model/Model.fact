namespace FacetedWorlds.MyCon.Model;

fact Identity {
key:
    string anonymousId;

query:
    DisableToastNotification* isToastNotificationDisabled {
        DisableToastNotification d : d.identity = this
            where not d.isReenabled
    }
}

fact DisableToastNotification {
key:
    unique;
    Identity identity;

query:
    bool isReenabled {
        exists EnableToastNotification e : e.disable = this
    }
}

fact EnableToastNotification {
key:
    DisableToastNotification* disable;
}

fact Conference {
key:
	string id;

mutable:
    string name;

query:
	Day* days {
		Day d : d.conference = this
	}

	Track* tracks {
		Track t : t.conference = this
	}

	Session* sessions {
		Session s : s.conference = this
	}
}

fact Attendee {
key:
    Identity identity;
    Conference conference;
}

fact Day {
key:
	Conference conference;
	date conferenceDate;

query:
	Time* times {
		Time t : t.day = this
	}
}

fact Time {
key:
    Day day;
    time start;

query:
	SessionPlace* availableSessions {
		SessionPlace sp : sp.place.placeTime = this
			where sp.isCurrent
	}
}

fact Slot {
key:
    Attendee attendee;
    Time slotTime;
}

fact Room {
key:
    Conference conference;
    string roomNumber;
}

fact Track {
key:
    Conference conference;
    string name;

query:
	SessionPlace* currentSessionPlaces {
		SessionPlace sp : sp.session.track = this
			where sp.isCurrent
	}
}

fact Speaker {
key:
    Conference conference;
    string name;

mutable:
	string imageUrl;
	string contact;
	string bio;
}

fact Place {
key:
    Time placeTime;
    Room room;
}

fact Level {
key:
	string name;
}

fact Session {
key:
    string id;
    Conference conference;
    Speaker speaker;
    Track? track;

mutable:
    string name;
	string description;
	Level level;

query:
	SessionPlace* currentSessionPlaces {
		SessionPlace sp : sp.session = this
			where sp.isCurrent
	}
}

fact SessionPlace {
key:
    Session session;
    Place place;
    SessionPlace* prior;

query:
	bool isCurrent {
		not exists SessionPlace next : next.prior = this
	}
}

fact Schedule {
key:
    Slot slot;
    SessionPlace sessionPlace;
    Schedule* prior;
}

fact Evaluation {
key:
    Schedule schedule;
}
